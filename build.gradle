group 'org.team401'
version '1.0'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.8

repositories {
    mavenCentral()

    maven {
        url "http://first.wpi.edu/FRC/roborio/maven/release"
    }
}

configurations {
    required
    compile.extendsFrom required
}

dependencies {
    required group: 'edu.wpi.first.wpilibj', name: 'athena', version: '2017.3.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    dependsOn configurations.runtime
    from {
        (configurations.runtime - configurations.required).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

bintray {
    user = project.hasProperty('user') ? project.property('user') : ""
    key = project.hasProperty('key') ? project.property('key'): ""
    configurations = ['archives']
    pkg {
        repo = "SnakeSkin"
        name = "LightLink-Java"
        userOrg = "team401"
        licenses = ["GPL-3.0"]
        vcsUrl = "https://github.com/team401/LightLink-Java"
        version {
            name = this.version
        }
    }
}